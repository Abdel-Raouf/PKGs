# Contributor: Dongsheng Cai <http://dongsheng.org>
# Contributor: codekoala < aur(at)cloudlery.com >
# Maintainer: ava1ar < mail(at)ava1ar(dot)info >

pkgname=btsync
pkgver=1.1.27
pkgrel=1
pkgdesc="BitTorrent Sync - automatically sync files via secure, distributed technology"
license=("custom:btsync")
arch=('i686' 'x86_64' 'arm' 'armv6h' 'armv7h')
url="http://labs.bittorrent.com/experiments/sync.html"
install=btsync.install
backup=('etc/btsync/btsync.conf')
source=(license.html::http://www.bittorrent.com/legal/terms-of-use btsync.service btsync@.service)
sha1sums=('SKIP' 'a9f81b52eff87ed5879262bc7302cbc2486a8040' 'a792b35229f54aeda5b4a4801d896960da69493c')

if [ "$CARCH" == x86_64 ]; then
	source+=("http://syncapp.bittorrent.com/${pkgver}/btsync_x64-${pkgver}.tar.gz")
	sha1sums+=('3152ee36c58ada4accaef61c3f53d8450833706b')
elif [ "$CARCH" == i686 ]; then
	source+=("http://syncapp.bittorrent.com/${pkgver}/btsync_i386-${pkgver}.tar.gz")
	sha1sums+=('4544b43d685fade09e7e384f2bc3c24fcbf2073c')
elif [ "$CARCH" == arm ] || [ "$CARCH" == armv6h ] || [ "$CARCH" == armv7h ]; then
	source+=("http://syncapp.bittorrent.com/${pkgver}/btsync_arm-${pkgver}.tar.gz")
	sha1sums+=('6e8d48bb317fdbf006c04abbbb4808e88f96e379')
fi

package() {
	cd "${srcdir}"
	install -Dm644 license.html "${pkgdir}/usr/share/licenses/${pkgname}/license.html"
	install -Dm755 btsync "${pkgdir}/usr/bin/btsync"
	# install system-wide configand systemd unit
	./btsync --dump-sample-config | sed 's:/home/user/\.sync:/var/lib/btsync:g' > btsync.conf
	install -Dm644 btsync.conf "${pkgdir}/etc/btsync/btsync.conf"
	install -Dm644 btsync.service "${pkgdir}/usr/lib/systemd/system/btsync.service"
	# install user-specific systemd unit
	install -Dm644 btsync@.service "${pkgdir}/usr/lib/systemd/system/btsync@.service"
}
