# $Id$
# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Mathias Stearn <mathias@10gen.com>
# Contributor: Alec Thomas

pkgname=mongodb-arm
provides=('mongodb')
pkgver=2.4.1
pkgrel=1
pkgdesc='A high-performance, open source, schema-free document-oriented database'
arch=('i686' 'x86_64' 'armv7h')
url='http://www.mongodb.org'
license=('AGPL3')
depends=('boost-libs' 'pcre' 'snappy' 'openssl' 'gperftools') # 'v8')
makedepends=('scons' 'boost' 'readline' 'ncurses' 'libpcap') # 'cyrus-sasl')
checkdepends=('python2-pymongo')
optdepends=('libpcap: needed for mongosniff')
backup=('etc/mongodb.conf')
install=mongodb.install
source=("$pkgname"::"git://github.com/wtfuzz/mongo-arm.git#branch=v$pkgver"
        'mongodb.conf' 'mongodb.service' 'removeWerror.patch')

build() {
  # fucking mongo aint no fun to package
  export SCONSFLAGS="$MAKEFLAGS"

  cd ${srcdir}/${pkgname}

  # failed to build with -Werror since 2.4.0
  patch -Np1 -i "$srcdir/removeWerror.patch"

  scons all \
    --use-system-boost \
    --use-system-pcre \
    --use-system-snappy \
    --use-system-tcmalloc \
    --ssl \
    --sharedclient
    # --use-system-v8
    # --use-sasl-client
}

<<COMMENT
check() {
  export SCONSFLAGS="$MAKEFLAGS"

  cd mongodb-src-r${pkgver}

  scons smokeAll --smokedbprefix=$srcdir
}
COMMENT

package() {
  export SCONSFLAGS="$MAKEFLAGS"

  cd ${srcdir}/${pkgname}

  scons install \
    --use-system-boost \
    --use-system-pcre \
    --use-system-snappy \
    --use-system-tcmalloc \
    --ssl \
    --sharedclient \
    --full \
    --prefix="$pkgdir/usr"
    # --use-system-v8
    # --use-sasl-client

  rm "$pkgdir/usr/lib/libmongoclient.a"

  install -Dm644 "$srcdir/mongodb.conf" "$pkgdir/etc/mongodb.conf"
  install -Dm644 "$srcdir/mongodb.service" "$pkgdir/usr/lib/systemd/system/mongodb.service"
  install -dm700 "$pkgdir/var/lib/mongodb"
  install -dm755 "$pkgdir/var/log/mongodb"
}

sha512sums=('SKIP'
            '05dead727d3ea5fe8af1a3c3888693f6b3e2b8cb7f197a5d793352e10d2c524e96c9a5c55ad2e88c1114643a9612ec0b26a2574b48a5260a9b51ec8941461f1c'
            '177251404b2e818ae2b546fe8b13cb76e348c99e85c7bef22a04b0f07b600fd515a309ede50214f4198594388a6d2b31f46e945b9dae84aabb4dfa13b1123bb9'
            '3cfb1bd508442daf470379840c9d1a89cf4f3e042925c0f1e87ad64cdcc83aebb27e287a221226e6d995eeec33b7047dc3e8f9b5669ac94ecb4fa3fcb0477732')
